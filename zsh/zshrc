# Lines configured by zsh-newuser-install
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt appendhistory extendedglob notify
bindkey -e
# End of lines configured by zsh-newuser-install
# The following lines were added by compinstall
zstyle :compinstall filename '/home/bol/.zshrc'

autoload -Uz compinit
compinit
# End of lines added by compinstall

# Environment variables
export EDITOR=/usr/bin/vim
export MAIL=/var/mail/$USER

# Load color support.
autoload -U colors
colors

# Load custom dircolors
[[ -f ~/.dir_colours ]] && eval $(dircolors ~/.dir_colours)

# Cache for completions
zstyle ':completion::complete:*' use-cache 1

if [[ -f /usr/share/git/completion/git-prompt.sh ]]; then
    source /usr/share/git/completion/git-prompt.sh
    HAVE_GIT=1
fi
function precmd {
    # The following 9 lines of code comes directly from Phil!'s ZSH prompt
    # http://aperiodic.net/phil/prompt/
    local TERMWIDTH
    (( TERMWIDTH = ${COLUMNS} - 1 ))

    local PROMPTSIZE=${#${(%):--- %D{%R.%S %a %b %d %Y}\! }}
    local PWDSIZE=${#${(%):-%~}}

    if [[ "$PROMPTSIZE + $PWDSIZE" -gt $TERMWIDTH ]]; then
	(( PR_PWDLEN = $TERMWIDTH - $PROMPTSIZE ))
    fi

    # check if jobs are executing
    if [[ $(jobs | wc -l) -gt 0 ]]; then
        PR_JOBS=" J:%j"
    else
        PR_JOBS=""
    fi

    # git support
    if [[ ${HAVE_GIT} ]]; then
        GIT_PS1_SHOWDIRTYSTATE=1
        GIT_PS1_SHOWUNTRACKEDFILES=1
        GIT_PS1_SHOWSTASHSTATE=1
        PR_GIT="$(__git_ps1)"
    else
        PR_GIT=""
    fi
}

setprompt () {
    # Need this, so the prompt will work
    setopt prompt_subst

    # let's load colors into our environment, then set them
    autoload colors

    if [[ "$terminfo[colors]" -gt 8 ]]; then
        colors
    fi

    # The variables are wrapped in %{%}. This should be the case for every
    # variable that does not contain space.
    for COLOR in RED GREEN YELLOW BLUE WHITE BLACK GREY CYAN; do
        eval PR_$COLOR='%{$fg_no_bold[${(L)COLOR}]%}'
        eval PR_BOLD_$COLOR='%{$fg_bold[${(L)COLOR}]%}'
    done

    # Finally, let's set the prompt
    PROMPT='${PR_BOLD_RED}<${PR_RED}<${PR_BOLD_BLACK}<${PR_BOLD_GREY} \
%D{%R.%S %a %b %d %Y}${PR_RED}!${PR_BOLD_WHITE}%${PR_PWDLEN}<...<%~%<< \

${PR_BOLD_RED}<${PR_RED}<${PR_BOLD_BLACK}<\
${PR_BOLD_GREY} %n@%m${PR_RED}!${PR_BOLD_WHITE}${PR_BOLD_RED}\
%(?.. E:%?)${PR_BOLD_BLUE}${PR_JOBS}${PR_GIT}${PR_BOLD_CYAN}\

${PR_BOLD_BLACK}>${PR_GREEN}>${PR_BOLD_GREEN}>\
%{${reset_color}%} '

    # Of course we need a matching continuation prompt
    PROMPT2='\
${PR_BOLD_BLACK}>${PR_GREEN}>${PR_BOLD_GREEN}>\
${PR_BOLD_WHITE} %_ ${PR_BOLD_BLACK}>${PR_GREEN}>\
${PR_BOLD_GREEN}>%{${reset_color}%} '
}

setprompt

# Setup which characters should be concidered to be words for alt-backspace word rubbing.
export WORDCHARS='*?-[]~=.&;!#$%^(){}<>/'
bindkey '^[[Z' reverse-menu-complete
bindkey '^[[A' history-search-backward
bindkey '^[[B' history-search-forward

DIRSTACKFILE="$HOME/.cache/zsh/dirs"
if [[ -f $DIRSTACKFILE ]] && [[ $#dirstack -eq 0 ]]; then
  dirstack=( ${(f)"$(< $DIRSTACKFILE)"} )
  [[ -d $dirstack[1] ]] && cd $dirstack[1]
fi
chpwd() {
  print -l $PWD ${(u)dirstack} >$DIRSTACKFILE
}

DIRSTACKSIZE=20

setopt autopushd pushdsilent pushdtohome

## Remove duplicate entries
setopt pushdignoredups

## This reverts the +/- operators.
setopt pushdminus

# Dont intercept rm *.
setopt rmstarsilent

# Unset keyring stuff if set
unset GNOME_KEYRING_CONTROL
unset GNOME_KEYRING_PID
unset GPG_AGENT_INFO

# Syntax highlightning
if [ -r /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]; then
	source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

# Aliases and functions for the shell
alias ls="ls --color=auto"
alias grep="grep --color=auto"
alias nmap-traceroute="sudo nmap -sn -Pn --traceroute --reason"
alias ssh="TERM=xterm-256color ssh"

function purge-ssh {
	host=$1
	getent hosts $host|awk -v host=$host '
		function remove(entry) {
			printf "Removing %s\n", entry
			cmd=sprintf("ssh-keygen -f ~/.ssh/known_hosts -R %s 2>/dev/null", entry)
			system(cmd)
		}

		{
			remove(host)
			remove($2)
			remove($1)
		}'
}

export PATH="$HOME/.local/bin/:$PATH"

# SDKMan
if [[ -x "$HOME/.sdkman/bin/sdkman-init.sh" ]]; then
  export SDKMAN_DIR="/home/bol/.sdkman"
  source "/home/bol/.sdkman/bin/sdkman-init.sh"
fi

# Rbenv
if [[ -x "$HOME/.rbenv/bin/rbenv" ]]; then
  export PATH="$HOME/.rbenv/bin:$PATH"
  eval "$(rbenv init -)"
fi

# Pyenv
if [[ -x "$HOME/.pyenv/bin/pyenv" ]]; then
  export PYENV_ROOT="$HOME/.pyenv"
  export PATH="$PYENV_ROOT/bin:$PATH"
  eval "$(pyenv init -)"
  eval "$(pyenv virtualenv-init -)"
fi
